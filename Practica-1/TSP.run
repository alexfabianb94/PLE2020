reset;
option randseed 10;

param n := 20;

set N := {1..n};
set A := {i in N, j in N: i != j};

param d := 1;

param xcoord{N} := round(Uniform(-100,100)); 
param ycoord{N} := round(Uniform(-100,100));

param c{(i,j) in A} := round(sqrt((xcoord[i] - xcoord[j])^2 + (ycoord[i] - ycoord[j])^2));

var x{A} binary;
var t{N} integer;

minimize z: sum {(i,j) in A} c[i,j] * x[i,j];
s.t.
	r1 {j in N}: sum {(i,j) in A} x[i,j] = 1;
	r2 {i in N}: sum {(i,j) in A} x[i,j] = 1;
	r3 {(i,j) in A: j != d}: t[j] >= t[i] + 1 - card(N)*(1-x[i,j]);
	r4: t[d] = 1;

option cplex_options "mipdisplay 5";
option gurobi_options "outlev 1";
option xpress_options "outlev 1";

option solver cplex;
# option solver gurobi;
# option solver xpress;

# option auxfiles rc;
# write msalida;

solve;

display {(i,j) in A: x[i,j]};
